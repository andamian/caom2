plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'application'
}

repositories {
    jcenter()
    mavenLocal()
}

sourceCompatibility = 1.7

group = 'org.opencadc'

version = '2.3.3'

mainClassName = 'ca.nrc.cadc.fits2caom2.Main'

dependencies {
    compile 'log4j:log4j:[1.2,1.3)'
    compile 'org.jdom:jdom2:[2,3)'
    compile 'gov.nasa.gsfc.heasarc:nom-tam-fits:[1,2)'
    
    compile 'org.opencadc:cadc-util:[1,2)'
    compile 'org.opencadc:caom2:[2.3.14,2.4.0)'
    compile 'org.opencadc:cadc-registry:[1,2)'
    compile 'org.opencadc:cadc-vos:[1,2)'

    testCompile 'junit:junit:[4,5)'
}

configurations {
    runtime.exclude group: 'org.springframework'

    intTestCompile.extendsFrom testCompile
    intTestRuntime.extendsFrom testRuntime
}

sourceSets {
    intTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/int-test/java')
        }

        // set the intTest resource directory
        resources.srcDir file('src/int-test/resources')
    }
}

tasks.withType(Test) {
    // reset the report destinations so that intTests go to their
    // own page
    reports.html.destination = file(reporting.baseDir.getAbsolutePath() + '/' + name)

    // Assign all Java system properties from
    // the command line to the tests
    systemProperties System.properties
}

task intTest(type: Test) {
    // set the configuration context
    testClassesDir = sourceSets.intTest.output.classesDir
    classpath = sourceSets.intTest.runtimeClasspath

    // run the tests always
    outputs.upToDateWhen { false }
}
